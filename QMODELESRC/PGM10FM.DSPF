     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(24)
     A                                      PRINT
     A                                      HELP
     A                                      HLPPNLGRP('XX' XX)
     A                                      HLPTITLE('Liste fonction')
     A                                      ALTHELP(CA01)
     A          R SFL01                     SFL
     A  08                                  SFLNXTCHG
     A            ZOPT           1   B  7  3
     A  12                                  DSPATR(RI PC)
     A                                      COLOR(WHT)
     A            ZCOD           3   O  7  6
     A            ZLIB          30   O  7 11
     A            ZDAC           8  0O  7 42
     A                                      EDTWRD('  /  /    ')
     A            ZDAM           8  0O  7 53
     A                                      EDTWRD('  /  /    ')
     A            ZNBEM          6  0O  7 67
     A                                      EDTCDE(Z)
     A            HNREC         10  0H
     A          R FORE1                     SFLCTL(SFL01)
     A                                      RTNCSRLOC(&WLICSR &WZOCSR &WPRCSR)
     A                                      OVERLAY
     A                                      PUTOVR
     A N07                                  ROLLUP(02)
     A                                      CSRLOC(WLIGN  WCOLN)
     A                                      CHANGE(91)
     A N04                                  ERASE(SFL01)
     A  04                                  SFLDSP
     A  05                                  SFLDSPCTL
     A  06                                  SFLCLR
     A  07                                  SFLEND
     A                                      SFLSIZ(16)
     A                                      SFLPAG(15)
     A                                      SFLCSRRRN(&WRACSR)
     A            WRACSR         5S 0H
     A            WRAN01         4S 0H      SFLRCDNBR
     A            ZPGM          10   O  1  2
     A                                      OVRDTA
     A                                  1 31'Liste des fonctions'
     A                                      COLOR(WHT)
     A            ZUSER         10   O  1 70
     A                                      OVRDTA
     A            ZDATE          8  0O  2  2
     A                                      EDTWRD('  /  /    ')
     A                                      OVRDTA
     A            ZHEUR          6  0O  2 14
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A            ZJOB          10   O  2 70
     A                                      OVRDTA
     A                                  4  3'Indiquez vos options, puis
     A                                       sur ENTREE.'
     A                                      COLOR(BLU)
     A                                  5  3'2=Modifier      3=Copier  -
     A                                      4=Supprimer    5=Afficher'
     A                                      COLOR(BLU)
     A                                  6  2'Opt Code Libellé        -
     A                                      Création   Modification  Nb e-
     A                                      mployés'
     A                                      COLOR(WHT)
     A            WLICSR        10A  H
     A            WZOCSR        10A  H
     A            WPRCSR         4S 0H
     A            WLIGN          3S 0H
     A            WCOLN          3S 0H
     A          R FORB1
     A                                      CF03
     A                                      CF05
     A                                      CF06
     A                                      CF12
     A                                      RTNCSRLOC(&WLICSR &WZOCSR &WPRCSR)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      CSRLOC(WLIGN  WCOLN)
     A                                      CHANGE(91)
     A                                 23  3'F3=Exit  F5=Réafficher  F6=Cr-
     A                                      éer      F12=Annuler'
     A                                      COLOR(BLU)
     A            WLICSR        10A  H
     A            WZOCSR        10A  H
     A            WPRCSR         4S 0H
     A            WLIGN          3S 0H
     A            WCOLN          3S 0H
     A          R WSFMSG                    SFL
     A                                      SFLMSGRCD(24)
     A            WMGKEY                    SFLMSGKEY
     A            WPGMQ                     SFLPGMQ
     A          R WSFCTL                    SFLCTL(WSFMSG)
     A                                      SFLSIZ(2)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A  10                                  SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  09                                  SFLEND
     A            WPGMQ                     SFLPGMQ
