     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(24)
     A                                      PRINT
     A                                      HELP
     A                                      HLPPNLGRP('PGMI01' PGMI01)
     A                                      HLPTITLE('Invite sur fonction')
     A                                      ALTHELP(CA01)
     A                                      CHECK(RLTB)
     A          R FO0001
     A                                      KEEP
     A                                      ASSUME
     A                                      PUTOVR
     A                                 24 80' '
     A          R SFL01                     SFL
     A  08                                  SFLNXTCHG
     A            ZOPT           1A  B  4  1
     A N13                                  DSPATR(UL)
     A  13                                  DSPATR(PR)
     A                                      COLOR(WHT)
     A            ZFCT           3A  O  4  3
     A            ZLIB          35A  O  4  8
     A            HNUMREC        8S 0H
     A          R FORE1                     SFLCTL(SFL01)
     A N07                                  ROLLUP(02)
     A                                      CHANGE(91)
     A                                      KEEP
     A                                      RTNCSRLOC(&WLICSR &WZOCSR &WPRCSR)
     A                                      CSRLOC(WLIGN      WCOLN)
     A                                      OVERLAY
     A                                      PUTOVR
     A N04                                  ERASE(SFL01)
     A                                      SFLCSRRRN(&WRACSR)
     A  04                                  SFLDSP
     A  05                                  SFLDSPCTL
     A  06                                  SFLCLR
     A  07                                  SFLEND
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      WINDOW(&WINLIG &WINCOL 17 42 *NOMSG-
     A                                      LIN)
     A                                      WDWBORDER((*COLOR TRQ) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A N11                                  USRRSTDSP
     A            WRACSR         5S 0H
     A            WRAN01         4S 0H      SFLRCDNBR
     A            ZPGM          10A  O  1  1OVRDTA
     A                                  1 16'Liste des Fonctions'
     A                                      COLOR(WHT)
     A                                  2  1'1=Choisir'
     A                                      COLOR(BLU)
     A  13                                  DSPATR(ND)
     A                                  3  1'O Code Libellé'
     A                                      COLOR(WHT)
     A            WLICSR        10A  H
     A            WZOCSR        10A  H
     A            WPRCSR         4S 0H
     A            WLIGN          3S 0H
     A            WCOLN          3S 0H
     A            WINLIG         2S 0P
     A            WINCOL         3S 0P
     A          R FORB1
     A                                      WINDOW(FORE1)
     A                                      CF03
     A                                      CF12
     A                                      RTNCSRLOC(&WLICSR &WZOCSR &WPRCSR)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      CSRLOC(WLIGN      WCOLN)
     A                                      CHANGE(91)
     A                                 17  1'F3=Exit     F12=Précédent'
     A                                      COLOR(BLU)
     A            WLICSR        10A  H
     A            WZOCSR        10A  H
     A            WPRCSR         4S 0H
     A            WLIGN          3S 0H
     A            WCOLN          3S 0H
     A          R WSFMSG                    SFL
     A                                      SFLMSGRCD(24)
     A            WMGKEY                    SFLMSGKEY
     A            WPGMQ                     SFLPGMQ
     A          R WSFCTL                    SFLCTL(WSFMSG)
     A                                      SFLSIZ(2)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A  10                                  SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  09                                  SFLEND
     A            WPGMQ                     SFLPGMQ
