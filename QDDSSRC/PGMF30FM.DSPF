     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(24)
     A                                      PRINT
     A                                      HELP
     A                                      HLPPNLGRP('PGMF30' PGMF30)
     A                                      HLPTITLE('Liste outq')
     A                                      ALTHELP(CA01)
     A          R SFL01                     SFL
     A  08                                  SFLNXTCHG
     A            ZMUSR         10   B  8  6
     A                                      COLOR(GRN)
     A            ZMDOC         10   B  8 17
     A                                      COLOR(GRN)
     A            ZMPRT         10   B  8 28
     A                                      COLOR(GRN)
     A            HNREC         10  0H
     A          R FORE1                     SFLCTL(SFL01)
     A                                      RTNCSRLOC(&WLICSR &WZOCSR &WPRCSR)
     A                                      OVERLAY
     A                                      PUTOVR
     A N07                                  ROLLUP(02)
     A                                      CSRLOC(WLIGN WCOLN)
     A                                      CHANGE(91)
     A N04                                  ERASE(SFL01)
     A  04                                  SFLDSP
     A  05                                  SFLDSPCTL
     A  06                                  SFLCLR
     A  07                                  SFLEND
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A                                      SFLCSRRRN(&WRACSR)
     A            WRACSR         5S 0H
     A            WRAN01         4S 0H      SFLRCDNBR
     A            ZPGM          10   O  1  2
     A                                      OVRDTA
     A                                  1 31'Liste des Imprimantes'
     A                                      COLOR(WHT)
     A            ZUSER         10   O  1 70
     A                                      OVRDTA
     A            ZDATE          8  0O  2  2
     A                                      EDTWRD('  /  /    ')
     A                                      OVRDTA
     A            ZHEUR          6  0O  2 14
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A            ZJOB          10   O  2 70
     A                                      OVRDTA
     A                                  5  2'Selection User/Doc/Print :'
     A            ZSUSR         10   B  5 29
     A                                      COLOR(WHT)
     A                                      OVRDTA
     A            ZSDOC         10   B  5 40
     A                                      COLOR(WHT)
     A                                      OVRDTA
     A            ZSPRT         10   B  5 51
     A                                      COLOR(WHT)
     A                                      OVRDTA
     A                                  7  2'    User       Document   Impriman-
     A                                      te                                 -
     A                                                '
     A                                      COLOR(WHT)
     A            WLICSR        10A  H
     A            WZOCSR        10A  H
     A            WPRCSR         4S 0H
     A            WLIGN          3S 0H
     A            WCOLN          3S 0H
     A          R FORB1
     A                                      CF03
     A                                      CF06
     A                                      CF15
     A                                      RTNCSRLOC(&WLICSR &WZOCSR &WPRCSR)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      CSRLOC(WLIGN WCOLN)
     A                                      CHANGE(91)
     A                                 23  3'F3=Exit     F6=Créer     F15=Netto-
     A                                      yage'
     A                                      COLOR(BLU)
     A            WLICSR        10A  H
     A            WZOCSR        10A  H
     A            WPRCSR         4S 0H
     A            WLIGN          3S 0H
     A            WCOLN          3S 0H
     A          R WSFMSG                    SFL
     A                                      SFLMSGRCD(24)
     A            WMGKEY                    SFLMSGKEY
     A            WPGMQ                     SFLPGMQ
     A          R WSFCTL                    SFLCTL(WSFMSG)
     A                                      SFLSIZ(2)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A  10                                  SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  09                                  SFLEND
     A            WPGMQ                     SFLPGMQ
