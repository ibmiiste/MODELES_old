     A          R ENTETE
     A                                      SKIPB(1)
     A                                      SPACEB(1)
     A                                     2'Programme d''édition :'
     A            ZPGM          10        23
     A                                   120'Page :'
     A            ZPAG           4  0    127
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     2'Demandé le :'
     A            ZDATE          8  0     15
     A                                      EDTWRD('  /  /    ')
     A                                    26'par :'
     A            ZUSR          10        32
     A                                      SPACEA(1)
     A            ZTITRE        30        46
     A          R COLON
     A                                      SPACEB(1)
     A                                     1'Matricule'
     A                                    12'Nom'
     A                                    38'Prénom'
     A                                    64'Adresse'
     A                                    95'CP'
     A                                   101'Ville'
     A          R DETLIG
     A                                      SPACEB(1)
     A            ZMATR         10  0      1
     A                                      EDTCDE(Z)
     A            ZNOMC         25        12
     A            ZPRE1C        25        38
     A            ZADR1C        30        64
     A            ZCPO           5        95
     A            ZVILC         30       101
     A          R DETGEN
     A                                      SPACEB(1)
     A                                     3'Matricule'
     A            ZMATR         10  0     13
     A                                      EDTCDE(Z)
     A                                    27'Fonction'
     A            ZCODF          3        36
     A            ZLIF          35        40
     A                                      SPACEA(1)
     A                                     9'Nom'
     A            ZNOM          35        13
     A                                    49'Prénom'
     A            ZPRE1         35        56
     A            ZPRE2         35        92
     A                                      SPACEA(1)
     A                                     5'Adresse'
     A            ZAD1          35        13
     A            ZAD2          35        49
     A            ZAD3          35        85
     A                                      SPACEA(1)
     A                                    10'CP'
     A            ZCPO           5        13
     A                                    19'Ville'
     A            ZVIL          35        25
     A                                    61'Téléphone'
     A            ZTEL          20        71
     A                                    92'Portable'
     A            ZPOR          20       101
     A                                      SPACEA(1)
     A                                    10'Date d''entrée'
     A            ZDAE           8  0     26
     A                                      EDTWRD('  /  /    ')
     A                                    41'Date de départ'
     A            ZDAS           8  0     57
     A                                      EDTWRD('  /  /    ')
     A                                      SPACEA(1)
     A                                    14'Création le'
     A            ZDAC           8  0     26
     A                                      EDTWRD('  /  /    ')
     A                                    37'à'
     A            ZHEC           6  0     39
     A                                      EDTWRD('  :  :  ')
     A                                    48'par'
     A            ZUSC          10        52
     A                                      SPACEA(1)
     A                                    10'Modification le'
     A            ZDAM           8  0     26
     A                                      EDTWRD('  /  /    ')
     A                                    37'à'
     A            ZHEM           6  0     39
     A                                      EDTWRD('  :  :  ')
     A                                    48'par'
     A            ZUSM          10        52
     A                                      SPACEA(1)
     A          R FIN
     A                                      SPACEB(1)
     A                                     2'Nombre total de lignes :'
     A            ZLIG          10  0     27
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
