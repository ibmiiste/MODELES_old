     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(24)
     A                                      PRINT
     A                                      HELP
     A                                      HLPPNLGRP('XX' XX)
     A                                      HLPTITLE('Détail fonction')
     A                                      ALTHELP(CA01)
     A          R FORE1
     A                                      CF03
     A                                      CF12
     A                                      CF15
     A                                      RTNCSRLOC(&WFMCUR &WZOCUR &WPRCUR)
     A                                      PUTOVR
     A                                      CSRLOC(WNMLGN WNMCOL)
     A                                      CHANGE(91)
     A            ZPGM          10   O  1  2
     A                                      OVRDTA
     A                                  1 31'Détail fonction'
     A                                      COLOR(WHT)
     A            ZUSER         10   O  1 70
     A                                      OVRDTA
     A            ZDATE          8  0O  2  2
     A                                      EDTWRD('  /  /    ')
     A                                      OVRDTA
     A            ZHEUR          6  0O  2 14
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A            ZMODE         30   O  2 27
     A                                      COLOR(WHT)
     A                                      OVRDTA
     A            ZJOB          10   O  2 70
     A                                      OVRDTA
     A                                  6 15'Fonction :'
     A            ZFCOD          3   B  6 27
     A  12                                  DSPATR(UL)
     A N12                                  DSPATR(PR)
     A                                      COLOR(WHT)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  7 16'Libellé :'
     A            ZFLIB         35   B  7 27
     A  13                                  DSPATR(UL)
     A N13                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      COLOR(WHT)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 18 13'Crée le :'
     A            ZDAC           8  0O 18 25
     A                                      EDTWRD('  /  /    ')
     A                                      COLOR(GRN)
     A                                      OVRDTA
     A                                 18 37'à :'
     A            ZHEC           6  0O 18 41
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                 18 51'par :'
     A            ZUSC          10   O 18 57
     A                                      OVRDTA
     A                                 19 10'Modifié le :'
     A            ZDAM           8  0O 19 25
     A                                      EDTWRD('  /  /    ')
     A                                      COLOR(GRN)
     A                                      OVRDTA
     A                                 19 37'à :'
     A            ZHEM           6  0O 19 41
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                 19 51'par :'
     A            ZUSM          10   O 19 57
     A                                      OVRDTA
     A                                 23  3'F3=Exit   F12=Annuler'
     A                                      COLOR(BLU)
     A            WFMCUR        10A  H
     A            WZOCUR        10A  H
     A            WPRCUR         4S 0H
     A            WNMLGN         3S 0H
     A            WNMCOL         3S 0H
     A          R WMGSFL                    SFL
     A                                      SFLMSGRCD(24)
     A            WMGKEY                    SFLMSGKEY
     A            WPGMQ                     SFLPGMQ
     A          R WMGCTL                    SFLCTL(WMGSFL)
     A                                      SFLSIZ(2)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A  10                                  SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  09                                  SFLEND
     A            WPGMQ                     SFLPGMQ
